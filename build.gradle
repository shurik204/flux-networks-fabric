plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT' // Fabric Loom
    id "io.github.p03w.machete" version "1.1.4" // Build jar compression

    id "com.modrinth.minotaur" version "2.+" // Modrinth publishing
    id "com.matthewprenger.cursegradle" version "1.+" // CurseForge publishing
    id 'maven-publish' // Maven publishing
}

version = "${minecraft_version}-${mod_version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        // Forge Config API port
        url = 'https://raw.githubusercontent.com/Fuzss/modresources/main/maven/'
    }
    maven {
        // Parchment mappings
        url 'https://maven.parchmentmc.org'
    }
    maven {
        // JEI
        url 'https://dvs1.progwml6.com/files/maven/'
    }
    maven {
        // The One Probe
        url 'https://maven.tterrag.com/'
    }
    maven {
        // Curios
        url 'https://maven.theillusivec4.top/'
    }
    maven {
        // JEI, Mekanism
        url 'https://modmaven.dev/'
    }
    maven {
        // Modern UI
        url 'https://maven.izzel.io/releases/'
    }
    maven {
        // EMI
        url "https://maven.terraformersmc.com/"
    }
    maven {
        url "https://maven.shedaniel.me/"
    }
    maven {
        name 'CurseMaven'
        url 'https://cursemaven.com/'
        content {
            includeGroup "curse.maven"
        }
    }
}



dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"

    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${loader_version}"

    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

//    modCompileOnly "curse.maven:gregtechceu-modern-890405:4676506" // GregTechCEu 1.20.1-1.0.10-forge

    // EMI
    modCompileOnly ("dev.emi:emi-fabric:$emi_version:api") { transitive = false }
    // JEI
    modCompileOnly ("mezz.jei:jei-$minecraft_version-fabric:$jei_version") { transitive = false }
    // REI
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:$rei_version"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:$rei_version"

    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
    // Forge Config API port
    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:8.0.0"

//    modImplementation "mezz.jei:jei-${minecraft_version}-core:${jei_version}"
//    runtimeOnly "mezz.jei:jei-${minecraft_version}-forge:${jei_version}"
//    compileOnly "mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"

    // TODO: Trinkets
//    compileOnly "top.theillusivec4.curios:curios-fabric:5.2.0-beta.3+1.20.1:api"
//    runtimeOnly "top.theillusivec4.curios:curios-fabric:5.2.0-beta.3+1.20.1"

    modImplementation(include("icyllis.modernui:ModernUI-Core:${modernui_core_version}") {
        exclude group: "org.apache.logging.log4j", module: "log4j-core"
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
        exclude group: "com.google.code.findbugs", module: "jsr305"
        exclude group: "org.jetbrains", module: "annotations"
        exclude group: "com.ibm.icu", module: "icu4j"
        exclude group: "it.unimi.dsi", module: "fastutil"
    })
    modImplementation(include("icyllis.modernui:ModernUI-Markdown:${modernui_core_version}") {
        exclude group: "org.apache.logging.log4j", module: "log4j-core"
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
        exclude group: "com.google.code.findbugs", module: "jsr305"
        exclude group: "org.jetbrains", module: "annotations"
        exclude group: "com.ibm.icu", module: "icu4j"
        exclude group: "it.unimi.dsi", module: "fastutil"
    })
    modImplementation "icyllis.modernui:ModernUI-Fabric:${minecraft_version}-${modernui_fabric_version}"

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}"}
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
